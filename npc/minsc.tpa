//Adding Boo Familiar

COPY ~%MOD_FOLDER%/npc/minsc/itm~ ~override~

COPY ~%MOD_FOLDER%/npc/minsc/cre/vpboo.cre~ ~override~ 
 SAY NAME1 #18275
 SAY NAME2 #18275
 SAY ATTACK1 ~~ [CBooAtt1]
 SAY ATTACK2 ~~ [CBooAtt2]
 SAY ATTACK3 ~~ [CBooAtt3]
 SAY HURT ~~ [CBooHurt]
 SAY SELECT_COMMON1 ~~ [CBooSel1]
 SAY SELECT_COMMON2 ~~ [CBooSel2]
 SAY SELECT_COMMON3 ~~ [CBooSel3]
 SAY SELECT_ACTION1 ~~ [CBooAct1]
 SAY SELECT_ACTION2 ~~ [CBooAct2]
 SAY SELECT_ACTION3 ~~ [CBooAct3]
 SAY CRITICAL_HIT ~~ [CBooCrHi]
 SAY CRITICAL_MISS ~~ [CBooCrMi]
 SAY SPELL_DISRUPTED ~~ [CBooSpFa]

//Spells

LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = SPPR104 destination = ~VPBOO11~ END // Detect Evil
LAF cd_clone_spell INT_VAR make_innate = 1 STR_VAR source = SPIN699 destination = ~VPBOO12~ END // Healing Lick
COPY ~%MOD_FOLDER%/npc/minsc/spl/vpboo13.spl~ ~override~

// Sounds

LAF HANDLE_AUDIO
STR_VAR
 audio_path = ~%MOD_FOLDER%/npc/minsc/audio/boo~
 oggdec_path = ~%MOD_FOLDER%/npc/minsc/audio~
 sox_path = ~%MOD_FOLDER%/npc/minsc/audio~
END

//Dialogue

COMPILE ~%MOD_FOLDER%/npc/minsc/dialogue/dialogue.d~

//Scripts 

COMPILE ~%MOD_FOLDER%/npc/minsc/scripts/vpboo.baf~

ACTION_FOR_EACH minsc IN minsc mins25 minsc_ bdminsc BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~%minsc%.bcs~ THEN BEGIN
        EXTEND_TOP ~%minsc%.BCS~ ~%MOD_FOLDER%/npc/minsc/scripts/minsc.baf~
    END
END

// Boo Item

COPY_EXISTING ~MISC84.ITM~ ~override~
    WRITE_LONG 0x18 (THIS | BIT2)
    WRITE_LONG 0x1e 0b11110101110110111111101111101010
    WRITE_BYTE 0x29 0b11111111
    WRITE_BYTE 0x2b 0b11111110
    WRITE_BYTE 0x2d 0b10110111
    WRITE_BYTE 0x2f 0b11111111
    WRITE_LONG 0x44 0x00000000
    WRITE_LONG 0x48 0x00000000
    WRITE_ASCII 0x44 ~GSACK01~
    READ_LONG 0x64 a_o
    READ_SHORT 0x68 a_n
    WRITE_BYTE a_o+0xc 4
    WRITE_SHORT a_o+0xe 20
    WRITE_SHORT a_o+0x22 1
    WRITE_SHORT a_o+0x24 1
    //WRITE_LONG a_o+0x18 (THIS | BIT11)
  LPF ADD_ITEM_EFFECT
    INT_VAR
        opcode = 67
        target = 1
        timing = 1
        header = 1
    STR_VAR
      resource = ~VPBOO~
  END
  LPF ADD_ITEM_EFFECT
    INT_VAR
        opcode = 174
        target = 1
        header = 1
    STR_VAR
        resource = ~CBOOACT2~
  END

COPY_EXISTING ~item_use.2da~ ~override~
    SET_2DA_ENTRY 1 3 4 ~2~